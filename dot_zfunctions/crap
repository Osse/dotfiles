local -a files nukes

if (( $#@ )); then
    files=( $@ )
elif [[ ! -t 0 ]]; then
    while IFS= read -d ''; do
        files+=( $REPLY )
    done
else
    files=( *(.) )
fi

ls --color $files

print_help() {
    print -P '%B'
    print -c 'v: Vim' 'l: less' 'h: head' 'c: cat' 'f: file' \
        "n: no/next (don't delete)" '[yd]: yes/delete' \
        'D: delete all' 'q: quit' 'Q: quit and delete selected' '?: Show help'
    print -P '%b'
}

prompt() {
    local x
    read -q 'x?'"${(%):-%B$1%b}"
}

nukes=()

for f in $files; do
    while true; do
        read -k 1 "action?${(%):-%B$f:%b} "
        echo
        case $action in
            v) vim -- $f                  ;;
            l) less -- $f                 ;;
            h) head -- $f                 ;;
            c) cat -- $f                  ;;
            f) file -- $f                 ;;
            [yd]) nukes+=($f); break      ;;
            D) nukes=( $files ); break 2  ;;
            n) break                      ;;
            q) return 1                   ;;
            Q) break 2                    ;;
            \?) print_help                ;;
            *) :                          ;;
        esac
    done
done

echo

if (( $#nukes )); then
    print -Pl '%BTo be deleted:' '' $nukes '%b'
    prompt 'Really? ' && rm -- $nukes
else
    print 'Nothing to be deleted'
fi

# vim: ft=zsh
