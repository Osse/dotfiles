local -a files nukes

if (( $#@ )); then
    files=( $@ )
elif [[ ! -t 0 ]]; then
    while IFS= read -d ''; do
        files+=( $REPLY )
    done
else
    files=( *(.) )
fi

ls --color $files

print_help() {
    print -P '%B'
    print -c 'v: Vim' 'l: less' 'h: head' \
        "n: no/next (don't delete)" '[yd]: yes/delete' \
        'D: delete all' 'q: quit' 'Q: quit and delete selected' '?: Show help'
    print -P '%b'
}

prompt() {
    local x
    read -q 'x?'"${(%):-%B$1%b}"
}

nukes=()

for f in $files; do
    read -k 1 "action?${(%):-%B$f:%b} "
    echo
    case $action in
        v) vim -- $f                ;;
        l) less -- $f               ;;
        h) head -- $f               ;;
        [yd]) nukes+=($f); continue ;;
        D) nukes=( $files ); break  ;;
        n) continue                 ;;
        q) return 1                 ;;
        Q) break                    ;;
        \?) print_help              ;;
        *) :                        ;;
    esac
    prompt 'Delete? ' && nukes+=($f)
    echo
done

echo

if (( $#nukes )); then
    print -Pl '%BTo be deleted:' '' $nukes '%b'
    prompt 'Really? ' && rm -- $nukes
else
    print 'Nothing to be deleted'
fi

# vim: ft=zsh
